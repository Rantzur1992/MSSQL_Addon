group 'io.testproject'
version '1.0.1'

apply plugin: 'java'

// If necessary, update the location of TestProject SDK JAR file
def TP_SDK = System.getenv("TP_SDK")

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// Configurations
configurations {
    tpsdk
    compile.extendsFrom tpsdk
}

// JAR Task
jar {
    assert file("${TP_SDK}").exists() : "TestProject SDK JAR file was not found, please update the TP_SDK variable"
    archiveName "${rootProject.name}-${version}.jar"
    dependsOn configurations.runtime
    from {
        // Removes TestProject SDK from the final jar file
        (configurations.runtime - configurations.tpsdk).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    // Extract SDK version
    from {
        (configurations.tpsdk).collect {
            zipTree(it).matching {
                include 'testproject-sdk.properties'
            }
        }
    }
}

dependencies {
    tpsdk files("${TP_SDK}")
    // https://mvnrepository.com/artifact/org.json/json
    compile group: 'org.json', name: 'json', version: '20190722'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    // https://mvnrepository.com/artifact/javax.resource/javax.resource-api
    compile group: 'javax.resource', name: 'javax.resource-api', version: '1.7'
    // https://mvnrepository.com/artifact/com.ibm.db2.jcc/db2jcc
    compile group: 'com.ibm.db2.jcc', name: 'db2jcc', version: 'db2jcc4'

}